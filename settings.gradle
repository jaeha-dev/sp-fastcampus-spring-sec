rootProject.name = 'sp-fastcampus-spring-sec'

// component, web, server 폴더를 생성하고,
// 각 폴더에 build.gradle 파일과 하위 폴더(src/main, src/test)를 생성하는 스크립트이다.
["component", "web", "server"].each {
    def compDir = new File(rootDir, it)
    if (!compDir.exists()) {
        compDir.mkdirs()
    }

    compDir.eachDir { subDir ->
        def gradleFile = new File(subDir.absolutePath, "build.gradle")
        if (!gradleFile.exists()) {
            gradleFile.text =
                    """
                    dependencies {
                    }
                    """.stripIndent(20)
        }
        [
                "src/main/java/com/sp/fc",
                "src/main/resources",
                "src/test/java/com/sp/fc",
                "src/test/resources"
        ].each { srcDir ->
            def srcFolder = new File(subDir.absolutePath, srcDir)
            if (!srcFolder.exists()) {
                srcFolder.mkdirs()
            }
        }

        def projectName = ":${it}-${subDir.name}";
        include projectName
        project(projectName).projectDir = subDir
    }
}
